model "The 3-Echelon Multi-period Warehouse Location Problem with Uncertainty"
  uses "mmsystem", "mmjobs", "mmxprs", "mmsheet"

! ============================================================================
! Reading problem parameters
! ============================================================================
filename := "CaseStudyData.txt";
! filename := "CaseStudyData_80_40.txt";

declarations
  ! Number of customers
  nbCustomers: integer
  ! Number of candidate locations
  nbCandidates: integer
  ! Number of suppliers
  nbSuppliers: integer
  ! Number of product groups
  nbProductGroups: integer
  ! Number of vehicle types
  nbVehicleTypes: integer
  ! Number of periods
  nbPeriods: integer
  ! Number of scenarios
  nbScenarios: integer
end-declarations

! Read the various numbers from the file
initializations from filename
  nbCustomers nbCandidates nbSuppliers nbProductGroups nbVehicleTypes
  nbPeriods nbScenarios
end-initializations

declarations
  ! Set of customers
  Customers = 1..nbCustomers
  ! Set of candidate locations
  Candidates = 1..nbCandidates
  ! Set of suppliers
  Suppliers = 1..nbSuppliers
  ! Set of product groups
  Products = 1..nbProductGroups
  ! Set of vehicle types
  Vehicles = 1..nbVehicleTypes
  ! Set of periods
  Periods = 1..nbPeriods
  ! Set of scenarios
  Scenarios = 1..nbScenarios

  ! Vector of customer ids
  CustomerId: array(Customers) of string
  ! Vectors of customer coordinates
  CustomerEasting: array(Customers) of real
  CustomerNorthing: array(Customers) of real

  ! The overall customer demand in kilograms per product group
  Demand: array(Customers, Products) of integer
  ! The annual customer demand in kilograms per product group and period
  DemandPeriods: array(Customers, Products, Periods) of integer
  ! The annual customer demand in kilograms per product group, period, and scenario
  DemandPeriodScenarios: array(Customers, Products, Periods, Scenarios) of integer

  ! Vector of candidate ids
  CandidateId: array(Candidates) of string
  ! Vectors of candidate coordinates
  CandidateEasting: array(Candidates) of real
  CandidateNorthing: array(Candidates) of real

  ! Vector of supplier ids
  SupplierId: array(Suppliers) of integer
  ! Vectors of supplier coordinates
  SupplierEasting: array(Suppliers) of real
  SupplierNorthing: array(Suppliers) of real
  ! Vector of supplier product groups; recall that each supplier only provides one product group
  SupplierProductGroup: array(Suppliers) of integer
  ! Vector of supplier capacity given in kilograms per year
  SupplierCapacity: array(Suppliers) of real
  ! Vector of supplier vehicle types
  SupplierVehicleType: array(Suppliers) of integer

  ! Setup costs for warehouses
  Setup: array(Candidates) of integer
  ! Operating costs for warehouses
  Operating: array(Candidates) of integer
  ! Capacity for warehouses
  Capacity: array(Candidates) of integer

  ! Distance matrix between candidate locations and customers
  DistanceCandidateCustomer: array(Candidates,Customers) of real
  ! Distance matrix between candidate locations and suppliers
  DistanceCandidateSupplier: array(Candidates,Suppliers) of real
  ! Matrix of transportation costs between candidate locations and customers
  CostCandidateCustomers: array(Candidates,Customers) of real
  ! Matrix of transportation costs between candidate locations and suppliers
  CostCandidateSupplier: array(Candidates,Suppliers) of real

  ! Vehicle related data. The vehicles are indexed 1, 2, and 3, where 1 stands
  ! for 18t trucks, 2 for 7.5t lorries, and 3 for 3.5t vans.
  ! The vehicle capacity in tonnes
  VehicleCapacity: array(Vehicles) of real
  ! The overall cost in pounds per mile travelled
  VehicleCostPerMileOverall: array(Vehicles) of real
  ! The overall cost in pounds per mile and tonne transported
  VehicleCostPerMileAndTonneOverall: array(Vehicles) of real
  ! The CO2 emission in kilograms per mile and tonne transported
  VehicleCO2PerMileAndTonne: array(Vehicles) of real
end-declarations

! Read data
initializations from filename
  CustomerId CustomerEasting CustomerNorthing
  CandidateId CandidateEasting CandidateNorthing
  SupplierId SupplierEasting SupplierNorthing
  SupplierProductGroup SupplierCapacity SupplierVehicleType
  Setup Operating Capacity
  Demand as "CustomerDemand"
  DemandPeriods as "CustomerDemandPeriods"
  DemandPeriodScenarios as "CustomerDemandPeriodScenarios"
  DistanceCandidateSupplier DistanceCandidateCustomer
  VehicleCapacity VehicleCostPerMileOverall VehicleCostPerMileAndTonneOverall
end-initializations


! ==================================================================================================
! Data preparation
! ==================================================================================================

! Transports between suppliers and locations use either 7.5t or 18t trucks, depending on the
! vehicle type. Make sure to convert from tonne to kilogram.
! --------------------------------------------------------------------------------------------------
forall(j in Candidates, k in Suppliers) do
  if(SupplierVehicleType(k) = 1) then
    CostCandidateSupplier(j,k) := 2 * DistanceCandidateSupplier(j,k) * VehicleCostPerMileAndTonneOverall(1) / 1000
  else
    CostCandidateSupplier(j,k) := 2 * DistanceCandidateSupplier(j,k) * VehicleCostPerMileAndTonneOverall(2) / 1000
  end-if
end-do

! Transports between locations and customers use 3.5t vans.
! Make sure to convert from tonne to kilogram.
! --------------------------------------------------------------------------------------------------
forall(j in Candidates, i in Customers)
  CostCandidateCustomers(j,i) := 2 * DistanceCandidateCustomer(j,i) * VehicleCostPerMileAndTonneOverall(3) / 1000

! To write data to csv if you want anything in python
! initializations to "mmsheet.csv:Demand.csv"
!   Demand as 'skiph+;[A:C](Customer,Product,Demand)'
! end-initializations
! initializations to "mmsheet.csv:DemandPeriods.csv"
! DemandPeriods as 'skiph+;[A:D](Customer,Product,Period,Demand)'
! end-initializations
! initializations to "mmsheet.csv:DemandPeriodScenarios.csv"
! DemandPeriodScenarios as 'skiph+;[A:E](Customer,Product,Period,Scenario,Demand)'
! end-initializations

! ==================================================================================================
! Variables Decalration
! ==================================================================================================
declarations
  ! A binary shows whether a warehouse w can cover for a customer c. This is a result from clustering.
  CanCover: array(Candidates, Customers) of integer

  ! Whether warehouse w covers a demand for product p of customer c
  x: array(Candidates, Customers, Products, Periods) of mpvar
  ! A binary saying whether a warehouse w is setup on period t
  y: array(Candidates, Periods) of mpvar
  ! A binary showing whether a warehouse w is open in period t
  o: array(Candidates) of mpvar

  ! How many kg a warehouse w send product p to customer c in period t
  v: array(Candidates, Customers, Products, Periods) of mpvar

  ! How many kg a supplier s supplies for warehouse w
  z: array(Candidates, Suppliers, Products, Periods) of mpvar
end-declarations

initializations from "cluster_vector.txt"
  CanCover
end-initializations

! ! ==================================================================================================
! ! Constraints Old Version
! ! ==================================================================================================
! forall(w in Candidates, t in Periods) do
!   y(w,t) is_binary
!   o(w,t) is_binary
! end-do
! forall(w in Candidates, c in Customers, p in Products, t in Periods) x(w,c,p,t) is_binary

! ! A customer c can be covered by a warehouse w if and only if that warehosue is open and can cover for that customer
! forall(w in Candidates, c in Customers, p in Products, t in Periods) x(w,c,p,t) <= o(w,t) * CanCover(w,c)

! ! A customer c should be covered by only one warehouse w
! forall(c in Customers, p in Products, t in Periods) sum(w in Candidates) x(w,c,p,t) = 1

! ! Amount of supplies for product p sent to warehouse w from supplier s should not exceed the supplier limit
! ! If the supplier does not provide that product p, there should not be any amount of supply of that product p sent
! ! (i.e. should be 0)
! forall(s in Suppliers, t in Periods) do
!   forall(p in Products) do
!     if (SupplierProductGroup(s) = p) then
!       ! if the supplier provides this product type
!       sum(w in Candidates) z(w,s,p,t) <= SupplierCapacity(s)
!     else
!       ! if supplier does not provide this product type
!       sum(w in Candidates) z(w,s,p,t) = 0
!     end-if
!   end-do
! end-do

! ! A supply for product p to warehouse w shoud be equal to the demand of product p covered by warehouse w
! forall(w in Candidates, p in Products, t in Periods) sum(s in Suppliers) z(w,s,p,t) = sum(c in Customers) DemandPeriods(c,p,t) * CanCover(w,c) * x(w,c,p,t)

! ! Each warehouse has its own capacity
! ! Limit the warehouse capacity by suppliers
! forall(w in Candidates, t in Periods) sum(s in Suppliers, p in Products) z(w,s,p,t) <= Capacity(w)
! ! Alternatively use customer demands instead to limit the capacity
! ! forall(w in Candidates, t in Periods) sum(c in Customers, p in Products) Demand(c) * x(w,c,p,t) <= Capacity(w)

! ! If a warehouse w is already setup, it needs to be open for the subsequent period
! forall(w in Candidates, t in Periods) o(w,t) = sum(t1 in 1..t) y(w,t1)

! ! A warehouse w can be setup only at most once
! forall(w in Candidates) sum(t in Periods) y(w,t) <= 1

! ! ==================================================================================================
! ! Objective Old Version
! ! ==================================================================================================
! obj := sum(w in Candidates, t in Periods) Setup(w) * y(w,t) + ! setup cost
!       sum(w in Candidates, c in Customers, p in Products, t in Periods) x(w,c,p,t) * CostCandidateCustomers(w,c) * DemandPeriods(c,p,t) * 2 + ! customer transportation cost
!       sum(w in Candidates, s in Suppliers, p in Products, t in Periods) z(w,s,p,t) * CostCandidateSupplier(w,s) * 2 + ! supplier transportation cost
!       sum(w in Candidates, t in Periods) o(w,t) * Operating(w) ! operating cost per year

! ==================================================================================================
! Constraints New Version
! ==================================================================================================
forall(w in Candidates, t in Periods) do
  y(w,t) is_binary
  o(w) is_binary
end-do
forall(w in Candidates, c in Customers, p in Products, t in Periods) x(w,c,p,t) is_binary

forall(w in Candidates) do
  forall(t in Periods) do
    o(w) >= y(w,t)
    if(t > 1) then
      y(w,t) >= y(w,t-1)
    end-if
  end-do
end-do

! A customer c can be covered by a warehouse w if and only if that warehosue is open and can cover for that customer
forall(w in Candidates, c in Customers, p in Products, t in Periods) x(w,c,p,t) <= CanCover(w,c) * y(w,t)

! A customer c should be covered by only one warehouse w
forall(c in Customers, p in Products, t in Periods) sum(w in Candidates) x(w,c,p,t) = 1

forall(w in Candidates, c in Customers, p in Products, t in Periods) v(w,c,p,t) = DemandPeriods(c,p,t)*CanCover(w,c)* x(w,c,p,t)

forall(w in Candidates, t in Periods) sum(c in Candidates, p in Products) v(w,c,p,t) <= Capacity(w) * y(w,t)

forall(w in Candidates, p in Products, t in Periods) sum(s in Suppliers) z(w,s,p,t) = sum(c in Candidates) v(w,c,p,t)

! Amount of supplies for product p sent to warehouse w from supplier s should not exceed the supplier limit
! If the supplier does not provide that product p, there should not be any amount of supply of that product p sent
! (i.e. should be 0)
forall(s in Suppliers, t in Periods) do
  forall(p in Products) do
    if (SupplierProductGroup(s) = p) then
      ! if the supplier provides this product type
      sum(w in Candidates) z(w,s,p,t) <= SupplierCapacity(s)
    else
      ! if supplier does not provide this product type
      sum(w in Candidates) z(w,s,p,t) = 0
    end-if
  end-do
end-do

forall(s in Suppliers, t in Periods) do
  forall(p in Products) do
    forall(w in Candidates) do
      if (SupplierProductGroup(s) = p) then
        ! if the supplier provides this product type
        z(w,s,p,t) <= SupplierCapacity(s)
      else
        ! if supplier does not provide this product type
        z(w,s,p,t) = 0
      end-if
    end-do

  end-do
end-do

! ==================================================================================================
! Objective New Version
! ==================================================================================================
obj := sum(w in Candidates, t in Periods) Setup(w) * y(w,t) + ! setup cost
      sum(w in Candidates, c in Customers, p in Products, t in Periods) x(w,c,p,t) * CostCandidateCustomers(w,c) * DemandPeriods(c,p,t) * 2 + ! customer transportation cost
      sum(w in Candidates, s in Suppliers, p in Products, t in Periods) z(w,s,p,t) * CostCandidateSupplier(w,s) * 2 + ! supplier transportation cost
      sum(w in Candidates, t in Periods) o(w) * Operating(w) ! operating cost per year



! ==================================================================================================
! Optimization
! ==================================================================================================

setparam("XPRS_MAXTIME", 600)
setparam("XPRS_VERBOSE", true)
writeln("Optimization Started")
minimize(obj)
writeln("Optimization Done")

! ==================================================================================================
! Results
! ==================================================================================================
write("Selected Locations: ")
forall(t in Periods) do
  forall(w in Candidates | getsol(y(w,t)) > 0.001)
    writeln(w, " in period ", t)
end-do
writeln

forall(t in Periods) do
  writeln("Period: ", t)
  writeln("Warehouse utilization: ", sum(c in Customers, p in Products) DemandPeriods(c,p,t))
  forall(w in Candidates, p in Products | getsol(y(w,t)) > 0.001) do
    foo := sum(c in Customers) DemandPeriods(c,p,t) * getsol(x(w,c,p,t))
    writeln("  ", w, " ", "p: ", foo)
  end-do
end-do
writeln

forall(t in Periods) do
  writeln("Period: ", t)
  writeln("Supplier utilization: ", sum(s in Suppliers) SupplierCapacity(s))
  forall(s in Suppliers) do
    foo := sum(w in Candidates, p in Products) getsol(z(w,s,p,t))
    writeln("  ", s, ": ", foo, " (", SupplierCapacity(s), ")")
  end-do
end-do
writeln

forall(t in Periods) do
  writeln("Period: ", t)
  writeln("Supply allocations: ")
  forall(w in Candidates) do
    forall(s in Suppliers, p in Products | getsol(z(w,s,p,t)) > 0.001) do
      writeln("  ", w, " -> ", s, ": ", getsol(z(w,s,p,t)))
    end-do
  end-do
end-do
writeln; writeln

writeln("Setup costs: ", sum(w in Candidates, t in Periods) Setup(w)*y(w,t).sol)
writeln("Transportation costs warehouse - customers: ", sum(c in Customers, w in Candidates, p in Products, t in Periods) CostCandidateCustomers(w,c)*x(w,c,p,t).sol*DemandPeriods(c,p,t))
writeln("Transportation costs suppliers - warehouse: ", sum(w in Candidates, s in Suppliers, p in Products, t in Periods) CostCandidateSupplier(w,s)*z(w,s,p,t).sol)
writeln("Total costs: ", getobjval)

end-model
